@ECHO OFF
TITLE Creating WeekNumber installer...

:: VARIABLES
SET INSTALL_SCRIPT_FOLDER=%CD%
SET VERSION=%1
SET NSIS_INSTALL_FOLDER="C:\Program Files (x86)\NSIS"
SET INSTALLER="%INSTALL_SCRIPT_FOLDER%\WeekNumberInstaller - %VERSION%.exe"
SET INSTALL_LOG="%INSTALL_SCRIPT_FOLDER%\WeekNumberInstaller - %VERSION%.log"
SET INSTALL_SCRIPT="%INSTALL_SCRIPT_FOLDER%\WeekNumber.nsi"

:: CHECK IF COMPILER EXIST
IF NOT EXIST %NSIS_INSTALL_FOLDER%\makensis.exe CALL :ERROR 100

:: REMOVE EXISTING INSTALLER WITH SAME VERSION
IF EXIST %INSTALLER% DEL /F /Q %INSTALLER% >NUL 2>&1

:: COMPILE INSTALL SCRIPT
PUSHD
CD %NSIS_INSTALL_FOLDER%
@ECHO %DATE% %TIME% Compiling %INSTALL_SCRIPT% >%INSTALL_LOG%
makensis.exe /DVERSION=%VERSION% /V1 %INSTALL_SCRIPT%
SET RESULT=%ERRORLEVEL%
@ECHO %DATE% %TIME% Compile operation completed with result %RESULT% >>%INSTALL_LOG%
POPD

:: RETURN RESULT
IF EXIST %INSTALLER% EXIT %RESULT%
CALL :ERROR %RESULT%

:ERROR
SET ERROR_RESULT=%~1
IF %ERROR_RESULT%==100 (
	@ECHO %DATE% %TIME% NSIS not found. Installation script could not be compiled. >>%INSTALL_LOG%
) ELSE (
	@ECHO %DATE% %TIME% Installation script compilation failed with error code %ERROR_RESULT%. >>%INSTALL_LOG%
)
EXIT %ERROR_RESULT%